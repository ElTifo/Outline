plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.triplet.play' version '2.4.2'
}
apply from: 'ThemerConstants.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        minSdkVersion 25
        versionCode 290062
        versionName "35.4"

        buildConfigField "boolean", "ENFORCE_GOOGLE_PLAY_INSTALL", "true"
        buildConfigField "boolean", "SUPPORTS_THIRD_PARTY_SYSTEMS", "false"
        buildConfigField "boolean", "ENABLE_APP_BLACKLIST_CHECK", "false"
        buildConfigField "boolean", "ALLOW_THIRD_PARTY_SUBSTRATUM_BUILDS", "true"
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        etherealRelease {
            storeFile file("EtherealKey.jks")
            keyAlias "outlinedark"
            storePassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_STORE_PASSWORD
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_KEY_PASSWORD
        }
        outlineRelease {
            storeFile file("OutlineKey.jks")
            keyAlias "outlinekey"
            storePassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_STORE_PASSWORD
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_KEY_PASSWORD
        }
    }

    flavorDimensions  "theme"
    productFlavors {
        dark {
            dimension "theme"
            applicationId "com.schnettler.ethereal"
            signingConfig signingConfigs.etherealRelease

            buildConfigField "String", "BASE_64_LICENSE_KEY", "\"" + ETHEREAL_BASE_64_LICENSE_KEY + "\""
            buildConfigField "String", "APK_SIGNATURE_PRODUCTION", "\"" + ETHEREAL_APK_SIGNATURE_PRODUCTION + "\""
        }
        light {
            dimension "theme"
            applicationId "com.schnettler.outline"
            signingConfig signingConfigs.outlineRelease

            buildConfigField "String", "BASE_64_LICENSE_KEY", "\"" + OUTLINE_BASE_64_LICENSE_KEY + "\""
            buildConfigField "String", "APK_SIGNATURE_PRODUCTION", "\"" + OUTLINE_APK_SIGNATURE_PRODUCTION + "\""
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug

            buildConfigField "boolean", "ENFORCE_GOOGLE_PLAY_INSTALL", "false"
            buildConfigField "String", "BASE_64_LICENSE_KEY", "\"\""
            buildConfigField "String", "APK_SIGNATURE_PRODUCTION", "\"\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

play {
    serviceAccountCredentials = file("PlayAPI.json")
    track = "production"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.javiersantos:PiracyChecker:1.2.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}